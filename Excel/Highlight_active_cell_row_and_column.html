<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=shift_jis">
<title>[VBA] アクティブセルの行と列を強調表示する</title>

<!-- Start for 'google-code-prettify' -->
<link href="../prettify.css" rel="stylesheet" type="text/css">
<script src="../prettify.js" type="text/javascript"></script>
<!-- End for 'google-code-prettify' -->

<link rel="stylesheet" href="../design.css" type="text/css">
<style type="text/css">
.auto-style7 {
	font-weight: normal;
}
.auto-style8 {
	background-color: #FFFF00;
}
</style>
</head>
<body onload="prettyPrint();">
<h1 class="auto-style7">
[Excel VBA] アクティブセルの行と列を強調表示する</h1>

<p> Excel VBA を使って、アクティブセルの行と列を強調表示する方法について記載します。</p>
<h2> 目次</h2>
<p>
<a href="#1._アクティブセルの行と列を強調表示する">1. アクティブセルの行と列を強調表示する</a><br>
<a href="#2._タイトル列、タイトル行を選択したときのアクションを追加する">2. タイトル列、タイトル行を選択したときのアクションを追加する</a><br>
<a href="#3._タイトル列、タイトル行に独自色を付ける">3. タイトル列、タイトル行に独自色を付ける</a><br>
<a href="#4._各色を設定シートで設定可能にする">4. 各色を設定シートで設定可能にする</a><br>
<a href="#5._ダブルクリックした項目で抽出を行うマクロ">5. ダブルクリックした項目で抽出を行うマクロ</a><br>
<a href="#6._合計_欄の追加_と_行・列の挿入処理への対応">6. &quot;合計&quot;欄の追加 と 行・列の挿入処理への対応</a><br>
<a href="#参考">参考</a></p>
<p> &nbsp;</p>

<h2><a name="1._アクティブセルの行と列を強調表示する">1. アクティブセルの行と列を強調表示する</a></h2>
<p> [概要] 表を使いやすくするマクロとして、アクティブセルの行と列を強調表示するマクロを作成します。</p>
<p>[環境]</p>
<ul>
	<li>Excel 2016</li>
	<li>OS: Windows10 home</li>
</ul>
<p> [画面]</p>
<p> 機能は１つです。<br>（機能１）選択中のセルに対応する行と列を強調表示（色を付ける）する</p>
<p> 色を以下のようにします。<br>− 選択中のセル： 黄色 (6)<br>− 選択中のセル上下左右のセル： ライトイエロー (36)</p>
<p> 
<img alt="" src="Highlight_active_cell_row_and_column/img9.jpg"></p>
<p> 
&nbsp;</p>
<p>[&quot;アクティブセルの行と列を強調表示&quot; する部分のVBA]<br></p>
<pre class="prettyprint linenums">
' 概要： アクティブセルの行と列を強調表示する
' 説明： アクティブセルの行と列を強調表示します。
' 引数： Target         アクティブセル
' 著者： 木下英俊
' 履歴： 2017/12/08 新規作成
' 参考： 日経ソフトウェア 2018年1月号 − 実務ですぐに役立つ Excel VBA 第8回 表を使いやすくするマクロ
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False

    Dim r As Long, c As Long
    Dim er As Long, ec As Long

    ' ターゲットセルの位置を取得
    r = Target.Row
    c = Target.Column
    ' 最終行、最終列の位置を取得
    er = Cells(Rows.Count, 1).End(xlUp).Row
    ec = Cells(1, Columns.Count).End(xlToLeft).Column
    
    ' 全てのセルの背景をクリア
    Cells.Interior.ColorIndex = xlNone
    
    ' 選択中のセルが１つなら以下の処理を実施
    ' 補足：列や行選択をした場合には以下の処理を実行しない
    If Target.Cells.CountLarge = 1 Then
    
        ' 中央エリアのセルがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(3, 3), Cells(er - 1, ec - 1))) Is Nothing Then
<span class="auto-style8">            ' ターゲットセルの上下左右のセルの背景色をライトイエロー(36)にする</span>
<span class="auto-style8">            Range(Cells(r, 1), Cells(r, ec - 1)).Interior.ColorIndex = <strong>36</strong></span>
<span class="auto-style8">            Range(Cells(1, c), Cells(er - 1, c)).Interior.ColorIndex = <strong>36</strong></span>
<span class="auto-style8">            ' ターゲットセルの背景色を黄色(6)にする</span>
<span class="auto-style8">            Cells(r, c).Interior.ColorIndex = <strong>6</strong></span>
        End If
        
    End If

End Sub</pre>
	
<p>&nbsp;</p>
<p>[説明]<br>ー7行目： アクティブセル移動時のイベント処理です。（Worksheet_SelectionChange）<br>−21行目： 全てのセルの背景色（Interior）を無しにします。<br>
ー25行目： 選択中のセルが１つなら以下の処理を実行<br>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; 
行や列の選択をしたときは何も処理しないようにこの処理を入れています。<br>−30行目： 選択セルの行の色を色コード36（薄黄色）にします。<br>−31行目： 選択セルの列の色を色コード36（薄黄色）にします。<br>
−33行目： 選択セルの色を色コード6（黄色）にします。</p>
<p>&nbsp;</p>
<p>[サンプルプログラム]
<a href="Highlight_active_cell_row_and_column/Highlight_active_cell_row_and_column_part1.xlsm">
ダウンロード</a></p>
<p>
&nbsp;</p>
<h2>
<a name="2._タイトル列、タイトル行を選択したときのアクションを追加する">2. タイトル列、タイトル行を選択したときのアクションを追加する</a></h2>
<p>
上部や左側のタイトルエリアが選択された場合、期待する動作は列や行のみの強調表示と考えます。<br>選択セル（ターゲットセル）としてタイトルエリアが選択された場合に、行または列のみが強調表示されるようにVBAを修正します。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img12.jpg"></p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img13.jpg"></p>
<p>
&nbsp;</p>
<p>
VBAマクロを以下のように修正します。</p>
<pre class="prettyprint linenums">
' 概要： アクティブセルの行と列を強調表示する
' 説明： アクティブセルの行と列を強調表示します。
' 引数： Target         アクティブセル
' 著者： 木下英俊
' 履歴： 2017/12/08 新規作成
' 参考： 日経ソフトウェア 2018年1月号 − 実務ですぐに役立つ Excel VBA 第8回 表を使いやすくするマクロ
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False

    Dim r As Long, c As Long
    Dim er As Long, ec As Long

    ' ターゲットセルの位置を取得
    r = Target.Row
    c = Target.Column

    ' 最終行、最終列の位置を取得
    er = Cells(Rows.Count, 1).End(xlUp).Row
    ec = Cells(1, Columns.Count).End(xlToLeft).Column
    
    ' 全てのセルの背景をクリア
    Cells.Interior.ColorIndex = xlNone
    
    ' 選択中のセルが１つなら以下の処理を実施
    ' 補足：列や行選択をした場合には以下の処理を実行しない
    If Target.Cells.CountLarge = 1 Then
    
        ' 中央エリアのセルがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(3, 3), Cells(er - 1, ec - 1))) Is Nothing Then
            ' ターゲットセルの上下左右のセルの背景色をライトイエロー(36)にする
            Range(Cells(r, 1), Cells(r, ec - 1)).Interior.ColorIndex = 36
            Range(Cells(1, c), Cells(er - 1, c)).Interior.ColorIndex = 36
            ' ターゲットセルの背景色を黄色(6)にする
            Cells(r, c).Interior.ColorIndex = 6
        End If
        
<span class="auto-style8">        ' 右上エリアがアクティブだった場合の処理</span>
<span class="auto-style8">        If Not Intersect(Target, Range(Cells(1, 3), Cells(2, ec - 1))) Is Nothing Then</span>
<span class="auto-style8">            ' ターゲットセルと同じ列のみのセル背景色をライトイエロー(36)にする</span>
<span class="auto-style8">            Range(Cells(1, c), Cells(er - 1, c)).Interior.ColorIndex = <strong>36</strong></span>
<span class="auto-style8">        End If</span>
<span class="auto-style8">        </span>
<span class="auto-style8">        ' 左下エリアがアクティブだった場合の処理</span>
<span class="auto-style8">        If Not Intersect(Target, Range(Cells(3, 1), Cells(er - 1, 2))) Is Nothing Then</span>
<span class="auto-style8">            'ターゲットセルと同じ行のみのセル背景色をライトイエロー(36)にする</span>
<span class="auto-style8">            Range(Cells(r, 1), Cells(r, ec - 1)).Interior.ColorIndex = <strong>36</strong></span>
<span class="auto-style8">        End If</span>
    
    End If

End Sub</pre>
<p>
&nbsp;</p>
<p>
[説明]<br>ー
38行目： 選択セルが右上エリアであるかを確認します。<br>−40行目： 選択セルの列を強調表示する。<br>−43行名： 
選択セルが左下エリアであるかを確認します。<br>−45行目： 
選択セルの行を強調表示する。</p>
<p>
&nbsp;</p>
<h2>
<a name="3._タイトル列、タイトル行に独自色を付ける">3. タイトル列、タイトル行に独自色を付ける</a></h2>
<p>
ここまでの処理でだいぶんツールとして十分に使えるレベルになってきました。<br>
さて実際にツールとして使ってみると、タイトル部分などのセルが目立つように色を付けたくなってきます。<br>
ただこのままの状態でタイトル部分に色を付けると、セルを選択したときのVBA処理ですべてクリアされてしまい、せっかく色を付けたタイトル部分が全てクリアされてしまいます。<br>
タイトル部分に色を付けられるようにVBAを修正してみます。</p>
<p>
以下のように色分けしてみます。<br>− 左上エリア：&nbsp;&nbsp;&nbsp; うすい水色 (34)<br>ー 右上エリア：&nbsp;&nbsp;&nbsp; 
水色 (37)<br>ー 左下エリア：&nbsp;&nbsp;&nbsp; 水色 (37)</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img10.jpg"></p>
<p>
&nbsp;</p>
<p>
VBAマクロを以下のように修正します。</p>
<pre class="prettyprint linenums">
' 概要： アクティブセルの行と列を強調表示する
' 説明： アクティブセルの行と列を強調表示します。
' 引数： Target         アクティブセル
' 著者： 木下英俊
' 履歴： 2017/12/08 新規作成
' 参考： 日経ソフトウェア 2018年1月号 − 実務ですぐに役立つ Excel VBA 第8回 表を使いやすくするマクロ
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False

    Dim r As Long, c As Long
    Dim er As Long, ec As Long

    ' ターゲットセルの位置を取得
    r = Target.Row
    c = Target.Column
    ' 最終行、最終列の位置を取得
    er = Cells(Rows.Count, 1).End(xlUp).Row
    ec = Cells(1, Columns.Count).End(xlToLeft).Column
    
    ' 全てのセルの背景をクリア
<span class="auto-style8">    Range(Cells(3, 3), Cells(er - 1, ec - 1)).Interior.ColorIndex = <strong>xlNone</strong></span>
<span class="auto-style8">    Range(Cells(1, 1), Cells(2, 2)).Interior.ColorIndex = <strong>34</strong></span>
<span class="auto-style8">    Range(Cells(1, 3), Cells(2, ec - 1)).Interior.ColorIndex = <strong>37</strong></span>
<span class="auto-style8">    Range(Cells(3, 1), Cells(er - 1, 2)).Interior.ColorIndex = <strong>37</strong></span>
    
    ' 選択中のセルが１つなら以下の処理を実施
    ' 補足：列や行選択をした場合には以下の処理を実行しない
    If Target.Cells.CountLarge = 1 Then
    
        ' 中央エリアのセルがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(3, 3), Cells(er - 1, ec - 1))) Is Nothing Then
            Range(Cells(r, 1), Cells(r, ec - 1)).Interior.ColorIndex = 36
            Range(Cells(1, c), Cells(er - 1, c)).Interior.ColorIndex = 36
            Cells(r, c).Interior.ColorIndex = 6
        End If
        
        ' 上側エリアがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(1, 3), Cells(2, ec - 1))) Is Nothing Then
            Range(Cells(1, c), Cells(er - 1, c)).Interior.ColorIndex = 36
        End If
        
        ' 左側エリアがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(3, 1), Cells(er - 1, 2))) Is Nothing Then
            Range(Cells(r, 1), Cells(r, ec - 1)).Interior.ColorIndex = 36
        End If
    
    End If

End Sub</pre>
<p>
&nbsp;</p>
<p>
[説明]<br>ー 21行目： 中央エリアの背景色をクリアします。<br>ー 22行目： 左上エリアの背景色を薄水色(34)にします。<br>ー 23行目： 
右上エリアの背景色を水色(37)にします。<br>ー 24行目： 左下エリアの背景色を水色(37)にします。</p>
<p>
&nbsp;</p>
<h2>
<a name="4._各色を設定シートで設定可能にする">4. 各色を設定シートで設定可能にする</a></h2>
<p>
ここまでくるとシートで使用している色コードを任意の色に設定変更できるようにしたくなります。<br>「設定」シート上で任意炉の色へ変更できるようにしましょう。</p>
<p>
また色コードだけの入力だと実際の色がわからないと不便です。<br>横に実サンプルとしてコードと色の対応を表示しておきましょう。</p>
<p>
下図が作成するサンプルです。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img11.jpg"></p>
<p>
&nbsp;</p>
<p>
これまで作成してきた
VBAマクロ を以下のように修正します。</p>
<pre class="prettyprint linenums">
' 概要： アクティブセルの行と列を強調表示する
' 説明： アクティブセルの行と列を強調表示します。
' 引数： Target         アクティブセル
' 著者： 木下英俊
' 履歴： 2017/12/08 新規作成
' 参考： 日経ソフトウェア 2018年1月号 − 実務ですぐに役立つ Excel VBA 第8回 表を使いやすくするマクロ
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False

    Dim r As Long, c As Long
    Dim er As Long, ec As Long

    ' ターゲットセルの位置を取得
    r = Target.Row
    c = Target.Column

    ' 最終行、最終列の位置を取得
    er = Cells(Rows.Count, 1).End(xlUp).Row
    ec = Cells(1, Columns.Count).End(xlToLeft).Column
    
    ' 全てのセルの背景をクリア
    Range(Cells(3, 3), Cells(er - 1, ec - 1)).Interior.ColorIndex = xlNone
    Range(Cells(1, 1), Cells(2, 2)).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c3&quot;).Value</span>
    Range(Cells(1, 3), Cells(2, ec - 1)).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c4&quot;).Value</span>
    Range(Cells(3, 1), Cells(er - 1, 2)).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c5&quot;).Value</span>
    
    ' 選択中のセルが１つなら以下の処理を実施
    ' 補足：列や行選択をした場合には以下の処理を実行しない
    If Target.Cells.CountLarge = 1 Then
    
        ' 中央エリアのセルがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(3, 3), Cells(er - 2, ec - 2))) Is Nothing Then
            Range(Cells(r, 1), Cells(r, ec - 1)).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c8&quot;).Value</span>
            Range(Cells(1, c), Cells(er - 1, c)).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c9&quot;).Value</span>
            Cells(r, c).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c7&quot;).Value</span>
        End If
        
        ' 上側エリアがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(1, 3), Cells(2, ec - 1))) Is Nothing Then
            Range(Cells(1, c), Cells(er - 1, c)).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c9&quot;).Value</span>
        End If
        
        ' 左側エリアがアクティブだった場合の処理
        If Not Intersect(Target, Range(Cells(3, 1), Cells(er - 1, 2))) Is Nothing Then
            Range(Cells(r, 1), Cells(r, ec - 1)).Interior.ColorIndex = <span class="auto-style8">Sheets(&quot;設定&quot;).Range(&quot;c8&quot;).Value</span>
        End If
        
    End If
    
End Sub</pre>
<p>
&nbsp;</p>
<p>
&quot;設定&quot;シート側のVBAマクロです。<br>
本処理は&quot;設定&quot;シート固有の処理なので、下図の「Sheet2(設定)」にVBAマクロを記載する必要があります。<br>
シートが有効になったときに処理するために、イベント&quot;Activate&quot;をトリガーとする処理として実装します。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img1F.jpg"></p>
<pre class="prettyprint linenums">
' 概要： &quot;設定&quot;シートを開いたときに ColorIndex テーブルに従った色を表示する
' 説明： &quot;設定&quot;シートのための処理です。
'        設定で使用する各セルの色付けコードに使用する色サンプルを実際に画面上で見られるようにすることを目的とする処理です。
' 著者： 木下英俊
' 履歴： 2018/01/27 新規作成
Private Sub Worksheet_Activate()

    Dim x As Long
    Dim y As Long
    
    For y = 4 To 9
        For x = 8 To 8 + 10
            If Cells(y, x) &lt;&gt; &quot;&quot; Then
                Cells(y, x).Interior.ColorIndex = Cells(y, x).Value
            End If
        Next x
    Next y

End Sub
</pre>
<p>
&nbsp;</p>
<p>
[サンプルプログラム]
<a href="Highlight_active_cell_row_and_column/Highlight_active_cell_row_and_column_part4.xlsm">
ダウンロード</a></p>
<p>
&nbsp;</p>
<h2>
5<a name="5._ダブルクリックした項目で抽出を行うマクロ">. ダブルクリックした項目で抽出を行うマクロ</a></h2>
<p>
行、列、のそれぞれタイトル部分(下図で青色の部分)をダブルクリックすると、対応する行・列の中に &quot;○&quot; または &quot;■&quot;,&quot;END&quot; が含まれているところのみをフィルターして表示する、というVBAを作成します。<br>
（&quot;END&quot;を含めているのは、これまでに作成してきた選択セルの上下、左右の行・列に色を付けるマクロが正常動作しなくなるからです。）<br>
下図で水色部分をダブルクリックするとフィルターを解除します。</p>
<p>
こちらの図がフィルターをしていないデフォルト状態です。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img3.jpg"></p>
<p>
&nbsp;</p>
<p>
こちらが「03 テスト」部分をダブルクリックしたときの状態です。<br>AutoFilter により実現しています。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img4.jpg"></p>
<p>
&nbsp;</p>
<p>
こちらの図が「07 項目7」をダブルクリックしたときの状態です。<br>こちらは Columns(i).Hidden プロパティで実現しています。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img5.jpg"></p>
<p>&nbsp;</p>
<p>以下にVBAコードを示します。</p>
<p>[ダブルクリックした項目で抽出を行うマクロ]</p>
<pre class="prettyprint linenums">

Const EndMark As String = &quot;END&quot;

' 概要： ワークシート有効化時のイベント処理
' 説明： ワークシート有効化時のイベント処理を実装します。
' 引数： 無し
' 著者： 木下英俊
' 履歴： 2017/12/30 新規作成
Private Sub Worksheet_Activate()
    lastRow = Cells(Rows.Count, 1).End(xlUp).Row
    lastColumn = Cells(1, Columns.Count).End(xlToLeft).Column
End Sub



' 概要： マウス左ダブルクリック時のイベント処理
' 説明： マウス左ダブルクリック時のイベント処理を実装します。
' 引数： Target         アクティブセル
'        Cancel         true：  true にすると本来の左ダブルクリックの挙動をキャンセルします。
'                       false： default。本来の左ダブルクリックの挙動を実行します。
' 著者： 木下英俊
' 履歴： 2017/12/30 新規作成
Private Sub Worksheet_BeforeDoubleClick(ByVal Target As Range, Cancel As Boolean)
    Application.ScreenUpdating = False
    
    ' 前処理
    Dim r As Long, c As Long
    Dim er As Long, ec As Long

    ' ターゲットセルの位置を取得
    r = Target.Row
    c = Target.Column
    
    ' 最終行、最終列の位置を取得
    er = Cells(Rows.Count, 1).End(xlUp).Row
    ec = Cells(1, Columns.Count).End(xlToLeft).Column
    
    If Target.Row &lt;= 2 Then
        Cancel = True
        
        ' フィルター解除
        If ActiveSheet.FilterMode = True Then
            <span class="auto-style8">ActiveSheet.ShowAllData</span>
        End If
        
        If Target.Column &gt;= 3 And Target.Column &lt; ec Then
            <span class="auto-style8">Range(&quot;A1&quot;).AutoFilter field:=Target.Column, Criteria1:=Array(&quot;○&quot;, &quot;■&quot;, EndMark), Operator:=xlFilterValues</span>
        End If
    End If
    
    If Target.Column &lt;= 2 Then
        Cancel = True
        
        ' フィルター解除
        <span class="auto-style8">Columns.Hidden = False</span>
        
        If Target.Row &gt;= 3 And Target.Column &lt; er Then
            For i = 3 To ec - 1
                flag = False
                If Cells(Target.Row, i).Value = &quot;○&quot; Then
                    flag = True
                End If
                If Cells(Target.Row, i).Value = &quot;■&quot; Then
                    flag = True
                End If
                If flag = False Then
                    <span class="auto-style8">Columns(i).Hidden = True</span>
                End If
            Next i
        End If
        
    End If
    
    
    ' 画面更新
    Call Worksheet_SelectionChange(Target)
    
    Application.ScreenUpdating = True
End Sub

</pre>
<p>
&nbsp;</p>
<p>
[サンプルプログラム]
<a href="Highlight_active_cell_row_and_column/Highlight_active_cell_row_and_column_part5.xlsm">ダウンロード</a></p>
<p>
&nbsp;</p>
<h2>
<a name="6._合計_欄の追加_と_行・列の挿入処理への対応">6. &quot;合計&quot; 欄の追加 と 行・列の挿入処理への対応</a></h2>
<p>
ここまで修正すると、次は集計対象である&quot;○&quot;と&quot;■&quot;の合計数を表示したくなってきました。<br>これを追加してみます。<br>これ自体は対象セルに 
&quot;countif&quot; の計算式を追加するだけできます。<br>しかしこの状態でこのエクセルマクロを使っていると、行や列を挿入するたびにそのセルに 合計 と END 
を手動で追加する必要を生じてとても面倒になりました。これもVBAで自動的に挿入されるように修正します。</p>
<p>
まずは対象のエクセルシートの行と列の末尾に「合計」を加えます。これはVBAではなくシートの修正です。<br>例えば３行目の合計欄には 
「=COUNTIF(C3:L3,&quot;○&quot;)+COUNTIF(C3:L3,&quot;■&quot;)」 という式を記載することでできます。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img6.jpg"></p>
<p>
&nbsp;</p>
<p>
この状態で行や列を選択した状態で右クリックメニューから「挿入」をクリックした場合に、自動的にい「合計」と「END」の挿入も行われるようにします。</p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img7.jpg"></p>
<p>
<img alt="" src="Highlight_active_cell_row_and_column/img8.jpg"></p>
<p>
&nbsp;</p>
<pre class="prettyprint linenums">
<code>' 概要： ワークシート変化時のイベント処理
' 説明： ワークシート変化時のイベント処理を実装します。具体的には以下の処理を実装します。
'        行挿入：
'        行削除：
'        列挿入：
'        列削除：
' 引数： Target         アクティブセル
' 著者： 木下英俊
' 履歴： 2017/12/30 新規作成
Private Sub Worksheet_Change(ByVal Target As Range)
    Application.ScreenUpdating = False
    Application.EnableEvents = False

    ' 前処理
    debugString = &quot;&quot;
    Dim r As Long, c As Long
    Dim er As Long, ec As Long

    ' ターゲットセルの位置を取得
    r = Target.Row
    c = Target.Column
    
    ' 最終行、最終列の位置を取得
    er = Cells(Rows.Count, 1).End(xlUp).Row
    ec = Cells(1, Columns.Count).End(xlToLeft).Column
    
    ' 行の挿入・削除を確認
    If er &lt;&gt; lastRow Then
        If er &lt; lastRow Then
            ' 行削除
            debugString = &quot;行削除&quot;
        Else
            ' 行挿入
            debugString = &quot;行挿入&quot;
            
            ' 末尾に&quot;END&quot;を挿入
            For i = 1 To er - lastRow
                Cells(r + i - 1, ec).Value = &quot;END&quot;
                Cells(r + i - 1, ec - 1).FormulaR1C1 = &quot;=COUNTIF(RC[&quot; &amp; -ec + 4 &amp; &quot;]:RC[-1],&quot;&quot;○&quot;&quot;)+COUNTIF(RC[&quot; &amp; -ec + 4 &amp; &quot;]:RC[-1],&quot;&quot;■&quot;&quot;)&quot;
            Next i
        End If
    End If
    
    ' 列の挿入・削除を確認
    If ec &lt;&gt; lastColumn Then
        If ec &lt; lastColumn Then
            ' 列削除
            debugString = &quot;列削除&quot;
        Else
            ' 列挿入
            debugString = &quot;列挿入&quot;
            
            ' 末尾に&quot;END&quot;を挿入
            For i = 1 To ec - lastColumn
                Cells(er, c + i - 1).Value = &quot;END&quot;
                Cells(er - 1, c + i - 1).FormulaR1C1 = &quot;=COUNTIF(R[&quot; &amp; -er + 4 &amp; &quot;]C:R[-1]C,&quot;&quot;○&quot;&quot;)+COUNTIF(R[&quot; &amp; -er + 4 &amp; &quot;]C:R[-1]C,&quot;&quot;■&quot;&quot;)&quot;
            Next i
        End If
    End If
    
    
    ' 行数、列数、の情報を更新
    lastRow = Cells(Rows.Count, 1).End(xlUp).Row
    lastColumn = Cells(1, Columns.Count).End(xlToLeft).Column
    
    Application.EnableEvents = True
    Application.ScreenUpdating = True
End Sub
</code></pre>
<p>
&nbsp;</p>
<p>
&nbsp;</p>
<p>
[サンプルプログラム]
<a href="Highlight_active_cell_row_and_column/Highlight_active_cell_row_and_column_part6.xlsm">ダウンロード</a></p>
<p>
&nbsp;</p>
<p>
&nbsp;</p>
<h2>
<a name="参考">参考</a></h2>
<ul>
	<li>日経ソフトウェア 2018年1月号 − 実務ですぐに役立つ Excel VBA 第8回 表を使いやすくするマクロ</li>
</ul>
<hr>
<p> <font face="Meiryo UI">記載： 木下英俊<br>2017年12月08日 新規作成<br>2018年01月27日 更新</font></p>
</body>
</html>
